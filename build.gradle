ext.oshCoreVersion = '1.4.0'
ext.compileSdkVersion = 21
ext.minSdkVersion = 21
ext.targetSdkVersion = 21
//ext.buildToolsVersion = "28.0.3"
ext.buildToolsVersion = "30.0.2"
version = oshCoreVersion

buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:7.0.1'
  }
}

subprojects {
  group = 'org.sensorhub'

  repositories {
    mavenCentral()
    google()
  }

  if (!project.projectDir.parent.endsWith('osh-android')) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    ext.details = null
    ext.pom = {}

    configurations {
      embedded
      compile {
        extendsFrom embedded
      }
    }
  }

  configurations.all {
    exclude group: 'org.sensorhub', module: 'sensorhub-comm-ble-dbus'
    resolutionStrategy.dependencySubstitution {
      // force use of local projects
      substitute module('org.sensorhub:sensorhub-core') with project(':sensorhub-core')
      substitute module('org.sensorhub:sensorhub-comm-ble') with project(':sensorhub-comm-ble')
      substitute module('org.sensorhub:sensorhub-service-swe') with project(':sensorhub-service-swe')
      substitute module('org.sensorhub:sensorhub-service-video') with project(':sensorhub-service-video')
      substitute module('org.vast.opengis:ogc-services-sps') with project(':ogc-services-sps')

      // override objenesis version to get new android instantiators
      substitute module('uk.com.robust-it:cloning:1.9.1') with module('uk.com.robust-it:cloning:1.9.10')
    }
  }

  version = oshCoreVersion
}

